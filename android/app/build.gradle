plugins {
    id 'com.android.application'
}

android {
    ndkVersion '27.1.12297006'
    compileSdk PROP_COMPILE_SDK_VERSION.toInteger()

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        applicationId 'org.github.krkr2'
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION

        ndk {
            jobs Runtime.runtime.availableProcessors()
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

        externalNativeBuild {
            cmake {
                def buildTypeName = {
                    return project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') } ? "Release" : "Debug"
                }
                targets 'krkr2'
                arguments "-DCMAKE_BUILD_TYPE=${buildTypeName()}"
                cppFlags '-frtti -fexceptions -fsigned-char'
            }
        }
        versionCode Integer.parseInt(new Date().format('yyyyMMddHH'))
        versionName '1.4.0'
    }

    signingConfigs {
        release {
            storeFile file('sign.keystore')
            def signPropsFile = file('sign.properties')
            if (signPropsFile.exists()) {
                Properties signProps = new Properties()
                signProps.load(new FileInputStream(signPropsFile))
                keyAlias signProps['SIGN_KEY_ALIAS']
                keyPassword signProps['SIGN_KEY_PASS']
                storePassword signProps['SIGN_STORE_PASS']
            } else {
                keyAlias System.getenv('SIGN_KEY_ALIAS')
                keyPassword System.getenv('SIGN_KEY_PASS')
                storePassword System.getenv('SIGN_STORE_PASS')
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['java']
            res.srcDirs = ['res']
            assets.srcDirs = ['../assets']
        }

    }

    externalNativeBuild {
        cmake {
            buildStagingDirectory '../../cmake-build-android'
            path '../../CMakeLists.txt'
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def ver = defaultConfig.versionName
            def abi = filters.find { it.filterType == "ABI" }?.identifier ?: "all"
            outputFileName = "${rootProject.name}-$ver-${abi}.apk"
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':cocos2dx')
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.6.1'
    //noinspection GradleDependency
    implementation 'androidx.documentfile:documentfile:1.0.1'
    //noinspection GradleDependency
    implementation 'androidx.activity:activity:1.2.3'
    //noinspection GradleDependency
    implementation 'androidx.fragment:fragment:1.3.4'

    configurations.configureEach {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'
        }
    }
}
